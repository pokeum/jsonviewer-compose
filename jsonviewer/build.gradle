plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

def majorNumber = 0
def minorNumber = 0
def patchNumber = 0

def VERSION_NAME = "$majorNumber.$minorNumber.$patchNumber"
def VERSION_CODE = majorNumber * 100000 + minorNumber * 1000 + patchNumber * 10

android {
    namespace           "kr.pokeum.jsonviewer_compose"
    compileSdkVersion   COMPILE_SDK_VERSION

    defaultConfig {
        minSdkVersion       MIN_SDK_VERSION
        targetSdkVersion    TARGET_SDK_VERSION

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
        }
        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
        }
    }
    compileOptions {
        sourceCompatibility JAVA_COMPILE_VERSION
        targetCompatibility JAVA_COMPILE_VERSION
    }
    kotlinOptions {
        jvmTarget = JVM_TARGET
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion  versions.compose
        kotlinCompilerVersion           versions.kotlin
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {

    implementation androidx.core
    implementation androidx.composeUi
    implementation androidx.composeMaterial
    implementation androidx.composeUiToolingPreview
    implementation androidx.constraintLayoutCompose

    testImplementation test.junit

    debugImplementation debug.composeUiTooling
    debugImplementation debug.composeUiTestManifest
}

publishing {
    publications {
        release(MavenPublication) {
            groupId     = "com.github.pokeum"
            artifactId  = "jsonviewer-compose"
            version     = VERSION_NAME

            afterEvaluate {
                from components.release
            }
        }
    }
}